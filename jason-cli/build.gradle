/*
   Jason Command Line Interpreter build file
*/

defaultTasks 'build'

apply plugin: 'java-library'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/resources'
        }
    }
}

repositories {
    mavenCentral()

    maven { url "https://raw.githubusercontent.com/jacamo-lang/mvn-repo/master" }
    maven { url 'https://repo.gradle.org/gradle/libs-releases' }
}

dependencies {
    api project(':interpreter')

    api 'info.picocli:picocli-shell-jline3:4.7.0'
    api 'org.jline:jline:3.22.0'
    api 'org.fusesource.jansi:jansi:2.4.0'

    api 'org.gradle:gradle-tooling-api:8.0.2'
    //testImplementation group: 'junit', name: 'junit', version: '4.12'

    implementation 'org.slf4j:slf4j-nop:1.7.21' // to avoid warning msgs of SLF4J
}

jar {
    archiveBaseName = "jason-cli-${jasonVersion}"
    manifest {
        attributes 'Main-Class': 'jason.cli.JasonCLI',
                   'Specification-Title': 'Jason CLI',
                   'Specification-Version': "${jasonVersion}",
                   'Implementation-Version': new Date().toString()
    }
}

task run (type: JavaExec, dependsOn: 'build') {
    mainClass = 'jason.cli.JasonCLI'
    standardInput = System.in
    classpath sourceSets.main.runtimeClasspath
}

task uberJar(type: Jar, dependsOn: ['classes', ':interpreter:jar']) {
    description 'creates a single runnable jar file with all dependencies'
    duplicatesStrategy 'exclude'

    manifest {
        attributes 'Main-Class': 'jason.cli.JasonCLI',
                   'Specification-Title': 'Jason CLI',
                   'Specification-Version': "${jasonVersion}",
                   'Implementation-Version': new Date().toString()
    }
    archiveBaseName = "jason-cli-all-${jasonVersion}"
    destinationDirectory = file('build/bin')
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task createBin (dependsOn: 'uberJar') {
    def wdir = 'build/bin'
    doFirst {
        exec {
            commandLine '../../src/main/resources/scripts/create-bin.sh', 'jason-cli-all-' + "${jasonVersion}" + '.jar'
            workingDir wdir
        }
    }
    doLast {
        delete fileTree(wdir) { include 'jason-cli-all-*' }
        delete fileTree(wdir) { include '*.sh' }
        copy {
            from 'build/bin'
            into 'build/jason-' + "${jasonVersion}"
        }
    }
}

task release (type: Zip, dependsOn: 'createBin') {
    def wdir = 'build/jason-' + "${jasonVersion}"
    from wdir
    archiveBaseName = 'jason-bin-'+ "${jasonVersion}"
}
