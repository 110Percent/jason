= Some Programming Patterns in Jason
:toc: right
:source-highlighter: coderay
:coderay-linenums-mode: inline
:icons: font
:prewrap!:

ifdef::env-github[]
:tip-caption: :bulb:
:note-caption: :information_source:
:important-caption: :heavy_exclamation_mark:
:caution-caption: :fire:
:warning-caption: :warning:
endif::[]


ifdef::env-github[:outfilesuffix: .adoc]

== Concurrency
=== Singleton Plan

Some times we want that a plan has only one intention running it. For instance, when the agent wants to control the movement of a robot, it will be chaotic if two intentions with different targets are controlling the robot.

The following code avoids two or more concurrent executions of plan `g`:

----
+!g : not .intend(g) <- dosomething.
+!g. // do nothing for g
----

If the agent has two events `+!g`, when the first is handled, the agent does not intend `g` and the first plan is selected and thus the agent now intends `g` avoiding this plan to be selected again.

Example:
----
e.
+!e
   <- for ( .range(I,0,5) ) { // creates 5 concurrent intentions for g
         !!g(I);
      }.

+!g(I) : not .intend(g(_))
   <- .wait(200);
      .print(I).
+!g(I).
----

Prints:
----
0
----

NOTE: The annotation `atomic` could be considered for the first plan. However it will constraint too much the agent execution: no other intention (even not related to `g`) will run until `+!g` finished. The reactivity of the agent is thus compromised.

=== "Queued" Singleton Plan

As for the singleton plan, we want that only one intention is executing the plan. However, when an intention has no access to the plan, it waits for the running intention to complete and then tries again to execute.
----
+!g : not .intend(g)
   <- dosomething;
      !!resume(g).     // resume other intentions waiting to execute
                       // must be a new intention (!!), othewise this plan will not be selected again
+!g
   <- .suspend;        // suspend this intention
      !!g.             // and try again (note that we have to use !!, otherwise this intention prevents the above plan to be selected)

+!resume(G)
   <- .resume(G).
----

Example:
----
!e.
+!e
   <- for ( .range(I,0,5)) {
         !!g(I);
      }.

+!g(I) : not .intend(g(_))
   <- .print(I);
      .wait(50);
      .print(I);
      !!resume(g(_)).
+!g(I)
   <- .suspend;
      !!g(I).
+!resume(G)
   <- .resume(G).
----

Prints:
----
0
0
4
4
5
5
2
2
3
3
1
1
----

NOTE: There is no interleaving among the execution of the intentions.

This pattern is used in the Santa Claus example that comes with Jason.

== Commitment

(TBD copy from paper about commitment and the directives)
